<!DOCTYPE html><html><head><title>Racing demo</title><meta http-equiv="content-type" content="text/html; charset=UTF-8"><meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no, minimal-ui=1"><meta charset="UTF-8"><meta name="mobile-web-app-capable" content="yes"><meta name="apple-mobile-web-app-capable" content="yes"><meta name="description" content=""><link rel="manifest" href="manifest.json"><link rel="icon" type="image/png" href="icon64.png"><link rel="apple-touch-icon" sizes="180x180" href="icon180.png"><link rel="icon" type="image/png" sizes="32x32" href="icon32.png"><link rel="icon" type="image/png" sizes="16x16" href="icon16.png"><style>html,body {
  margin: 0;
  padding: 0;
  background-color: #000;
  overflow:hidden;
  font-family: Verdana;
}
.noselect {
  -webkit-touch-callout: none;
  -webkit-user-select: none;
  -khtml-user-select: none;
  -moz-user-select: none;
  -ms-user-select: none;
  user-select: none;
}
#canvaswrapper {
  text-align: center ;
}
</style><style>@font-face { font-family: "BitCell" ; src: url("fonts/BitCell.ttf") format("truetype"); }</style><style>@font-face { font-family: "Blocktopia" ; src: url("fonts/Blocktopia.ttf") format("truetype"); }</style><script>window.fonts = ["BitCell","Blocktopia"]</script></head><body class="noselect custom-cursor" oncontextmenu="return false;"><div id="canvaswrapper"></div><script type="text/javascript">var resources = {"images":[{"file":"background.png","version":10,"size":2067,"properties":{"frames":1,"fps":5}},{"file":"bush.png","version":1,"size":841,"properties":{}},{"file":"car_ai_black.png","version":1,"size":1113,"properties":{}},{"file":"car_ai_black_left.png","version":1,"size":1122,"properties":{}},{"file":"car_ai_black_right.png","version":1,"size":1107,"properties":{}},{"file":"car_ai_blue.png","version":1,"size":1210,"properties":{}},{"file":"car_ai_blue_left.png","version":1,"size":1204,"properties":{}},{"file":"car_ai_blue_right.png","version":1,"size":1205,"properties":{}},{"file":"car_ai_green.png","version":1,"size":1214,"properties":{}},{"file":"car_ai_green_left.png","version":1,"size":1206,"properties":{}},{"file":"car_ai_green_right.png","version":1,"size":1211,"properties":{}},{"file":"car_ai_red.png","version":1,"size":1177,"properties":{}},{"file":"car_ai_red_left.png","version":1,"size":1179,"properties":{}},{"file":"car_ai_red_right.png","version":1,"size":1181,"properties":{}},{"file":"car_ai_white.png","version":1,"size":1185,"properties":{}},{"file":"car_ai_white_left.png","version":1,"size":1191,"properties":{}},{"file":"car_ai_white_right.png","version":1,"size":1173,"properties":{}},{"file":"car_ai_yellow.png","version":1,"size":1263,"properties":{}},{"file":"car_ai_yellow_left.png","version":1,"size":1245,"properties":{}},{"file":"car_ai_yellow_right.png","version":1,"size":1248,"properties":{}},{"file":"car_player.png","version":44,"size":1460,"properties":{}},{"file":"car_player_left.png","version":14,"size":1451,"properties":{}},{"file":"car_player_right.png","version":9,"size":1439,"properties":{}},{"file":"cloud_big.png","version":1,"size":513,"properties":{}},{"file":"cloud_med.png","version":1,"size":385,"properties":{}},{"file":"cloud_sml.png","version":1,"size":313,"properties":{}},{"file":"dead_bushes.png","version":1,"size":980,"properties":{}},{"file":"finishline1.png","version":1,"size":81,"properties":{}},{"file":"finishline2.png","version":7,"size":80,"properties":{}},{"file":"fog.png","version":16,"size":171,"properties":{}},{"file":"fog2.png","version":49,"size":95,"properties":{}},{"file":"icon.png","version":113,"size":897,"properties":{}},{"file":"left_turn.png","version":1,"size":548,"properties":{}},{"file":"left_u_turn.png","version":1,"size":571,"properties":{}},{"file":"palm_tree.png","version":3,"size":1406,"properties":{}},{"file":"poster.png","version":1,"size":81865,"properties":{"frames":1,"fps":5}},{"file":"right_turn.png","version":1,"size":566,"properties":{}},{"file":"right_u_turn.png","version":1,"size":567,"properties":{}},{"file":"road1.png","version":34,"size":87,"properties":{}},{"file":"road2.png","version":7,"size":83,"properties":{}},{"file":"rock_big.png","version":1,"size":982,"properties":{}},{"file":"rock_med1.png","version":1,"size":609,"properties":{}},{"file":"rock_med2.png","version":1,"size":541,"properties":{}},{"file":"rock_sml.png","version":1,"size":368,"properties":{}},{"file":"sharp_left.png","version":1,"size":346,"properties":{}},{"file":"sharp_right.png","version":1,"size":346,"properties":{}},{"file":"sign_bot.png","version":1,"size":222,"properties":{}},{"file":"sign_mid.png","version":1,"size":255,"properties":{}},{"file":"startline.png","version":4,"size":87,"properties":{}},{"file":"touch_arrow.png","version":21,"size":230,"properties":{}},{"file":"tree.png","version":1,"size":2495,"properties":{}}],"assets":[],"maps":{},"sounds":[],"music":[{"file":"joseycarlos.mp3","version":1,"size":5760919,"properties":{}}]};
var graphics = "M1";

</script><script type="text/javascript">var orientation = 'landscape' ;
var aspect = 'free' ;
var ms_libs = [] ;
window.skip_service_worker = true;
window.exported_project = true;
</script><script src="runner_v1_i.js"></script><script src="microengine.js"></script></body><script id="code" type="text/x-microscript">

drawBackground = function()
  screen.fillRect(0,0,screen.width,screen.height,"rgb(255,179,28)")
  screen.setLinearGradient(0,0,0,screen.height/2,"rgb(255,198,255)","#48C")
  screen.fillRect(0,screen.height/4,screen.width,screen.height/2)
  local h = fmod(horizon,200)
  for o = -screen.width/2-100 to screen.width/2+100 by 200
    screen.drawSprite("background",o+h,15,200,30)
  end
  for c in clouds
    screen.setAlpha(.5+(c.y-30)*.5/40)
    screen.drawSprite(c.name,c.x,c.y,c.size)
  end
end

drawRoad = function()
  local off = 0
  local off2 = -x
  local tt = 0
  for i = 0 to 100 by 1
    local dist = lineToDistance(i)
    local d = position+dist
    local width = 50/dist
    local t = readTrack(d)
    local road = if (d*100)%10<5 then "road1" else "road2" end
    if d%trackValue.length>trackValue.length-.1 then
      if (d*100)%2 <1 then road = "finishline1" else road = "finishline2" end
    elsif d%trackValue.length>trackValue.length-1 then
      if (d*100)%10 <8 then road = "road2" else road = "startline" end
    end
    tt += t*dist*dist*.005
    off += tt
    screen.drawSprite(road,0+off+off2*(100-i)/100,-100+i,width,1)
    track[i] = off+off2*(100-i)/100
    if i == 0 then turn = t end
  end
end

drawFog = function()
  screen.setAlpha(.5)
  screen.drawSprite("fog2",0,0,screen.width,screen.height/16)
end

drawCars = function()
  screen.setAlpha(1)
  for c in ai_cars
    local d = c.position-position
    if d<-trackValue.length/2 then
      d += trackValue.length
    end
    if d>0 then
      local scale = 1000/(d*120)
      local line = distanceToLine(d)
      local h = floor(line)
      local a = line-h
      h = max(0,min(99,h))
      local px = track[h]*(1-a)+track[h+1]*a
      local car = readTrack(c.position)
      if car+x-c.x>100 then car = 2 elsif car+x-c.x<-100 then car = 0 else car = 1 end
      //if c.x-x>100 then car -= 1 elsif c.x-x<-100 then car += 1 end
      screen.fillRect(c.x*scale*.012+px,-100+line+scale/4-scale/4,scale,scale/8,"rgba(0,0,0,.25)")
      screen.drawSprite("car_ai_"+c.color+["_left","","_right"][max(0,min(2,car))],c.x*scale*.012+px,-100+line+scale/4,scale,scale/2)
    end
  end
end

drawSideObjects = function()
  for i=25 to 1 by -1
    local pos = position*10+i
    local index = floor(pos)
    local delta = pos-index
    local lo = left_objects[index%left_objects.length]
    local ro = right_objects[index%left_objects.length]
    local distance = (index-position*10) // between 0 and 10
    local line = distanceToLine(distance/10)
    local width = 50/(distance/10)

    if distance<1 then continue end

    local size = 1000/(distance*70)*4
    local h = floor(line)
    local a = line-h
    h = max(0,min(99,h))
    local x = track[h]*(1-a)+track[h+1]*a
    
    screen.setAlpha(exp(-distance*.01)*min(1,(25-i)*.2))
    local s = size*2*lo.size
    if lo then screen.drawSprite(lo.sprite,width*(.65+lo.offset)+x,line-100+s/2*lo.ratio,s,s*lo.ratio) end
    s = size*2*ro.size
    if ro then screen.drawSprite(ro.sprite,-width*(.65+ro.offset)+x,line-100+s/2*ro.ratio,s,s*ro.ratio) end 
  end
end

drawPlayerCar = function()
  screen.setAlpha(1)
  
  local off = sin(position*80)*if abs(x)>230 then 1 else .25 end
  screen.fillRect(0,-90+off,82,20,"rgba(0,0,0,.3)")
  local car = if turn>100 then 2 elsif turn<-100 then 0 else 1 end
  car += -left+right
  car = ["car_player_left","car_player","car_player_right"][max(0,min(2,car))]
  screen.drawSprite(car,0,-75+off,80,40)
end

drawUI = function()
  screen.setAlpha(if right then 1 else .5 end)
  screen.setDrawScale(-1,1)
  screen.drawSprite("touch_arrow",screen.width/2-40,-60,30,30)
  screen.setDrawScale(1,1)
  screen.setAlpha(if left then 1 else .5 end)
  screen.drawSprite("touch_arrow",-screen.width/2+40,-60,30,30)
  screen.setAlpha(1)
  screen.setColor("#FFF")
  screen.setFont("Blocktopia")
  screen.drawText(floor(speed*6.2),screen.width/2-40,80,15)
  screen.drawText("Lap 2/4",-screen.width/2+40,80,10)
  screen.drawText("2nd",0,80,10)
end

draw = function()
  drawBackground()
  drawRoad()
  drawFog()
  drawCars()
  drawSideObjects()
  drawPlayerCar()
  drawUI()
end

init = function()
  speed = 20
  track = []
  horizon = 0
  position = trackValue.length-1
  x = 120
  
  ai_cars = []
  for i=10 to 1
    ai_cars.push( object
      x = 120-240*(i%2)
      position = trackValue.length-1+i*.1
      speed = 30+i
      color = ["green","blue","yellow","white","black","red"][i%6]
    end )
  end
  
  clouds = [
    object name="cloud_big" x = 120 y = 35 v = .02 size = 40 end
    object name="cloud_med" x = -120 y = 50 v = .04 size = 40 end,
    object name="cloud_sml" x = 120 y = 70 v = .06 size = 40 end,
  ]
end

title_update = function()
  if touch.press then 
    title = false 
    spooky.play()    
  end
end

init = function()

spooky = audio.playMusic("joseycarlos",100,100)


end

trackData = "===l===r===LR===RR==R=L=L=L======RR=RLR==="
trackValue = []
trackDic = object
  l = -150
  L = -300
  r = 150
  R = 300
end

DECOR = [
  object sprite = "palm_tree" size = 2.5 ratio=1 offset = 0 end
  object sprite = "bush" size = .5 ratio = 1 offset = 2 end
  object sprite = "rock_sml" size = .5 ratio = .5 offset = .2 end
  object sprite = "rock_sml" size = 1 ratio = .5 offset = .2 end
  object sprite = "tree" size = 2 ratio = 1.2 offset = 2 end
  object sprite = "rock_sml" size = 1 ratio = .5 offset = 2 end
  object sprite = "dead_bushes" size = 1 ratio = .25 offset = 2 end
  object sprite = "rock_med1" size = 1 ratio = 1 offset = 1 end
//  object sprite = "rock_med1" size = 1 ratio = 1 offset = .3 end
//  object sprite = "rock_med2" size = 1 ratio = 1 offset = .5 end
]

for i=0 to trackData.length-1
  trackValue[i] = trackDic[trackData[i]]
end

left_objects = []
right_objects = []

RIGHT_TURN_SIGN = object
  sprite = "right_turn"
  size = .75
  ratio = 1
end

RIGHT_ARROW_SIGN = object
  sprite = "sharp_right"
  size = .5
  ratio = .67
end

LEFT_TURN_SIGN = object
  sprite = "left_turn"
  size = .75
  ratio = 1
end

LEFT_ARROW_SIGN = object
  sprite = "sharp_left"
  size = .5
  ratio = .67
end

readTrack = function(x)
  local i1 = floor(x)
  local a = x-i1
  local i2 = (i1+1)

  local v1 = trackValue[i1%trackValue.length]
  local v2 = trackValue[i2%trackValue.length]
  a = a*a*(3-2*a)
  return v1*(1-a)+v2*a
end

for i=0 to trackData.length*10-1
  if random.next()<.2 then
    if readTrack(i/10)>200 then
      left_objects[i] = RIGHT_ARROW_SIGN
      right_objects[i] = RIGHT_ARROW_SIGN
      continue
    elsif readTrack(i/10)<-200 then
      left_objects[i] = LEFT_ARROW_SIGN
      right_objects[i] = LEFT_ARROW_SIGN
      continue
    elsif readTrack(i/10+1)>200 then
      left_objects[i] = RIGHT_TURN_SIGN
      right_objects[i] = RIGHT_TURN_SIGN
      continue
    elsif readTrack(i/10+1)<-200 then
      left_objects[i] = LEFT_TURN_SIGN
      right_objects[i] = LEFT_TURN_SIGN
      continue
    end
  end

  if random.next()<.5 then
    local o = DECOR[floor(pow(random.next(),2)*DECOR.length)]
    left_objects[i] = object
      sprite = o.sprite
      size = o.size //*(.8+.4*random.next())
      offset = o.offset*random.next()
      ratio = o.ratio
    end
  end
  if random.next()<.5 then
    local o = DECOR[floor(pow(random.next(),2)*DECOR.length)]
    right_objects[i] = object
      sprite = o.sprite
      size = o.size //*(.8+.4*random.next())
      offset = o.offset*random.next()
      ratio = o.ratio
    end
  end
end



update = function()
  speed += (60-speed)*.002
  left = right = 0
  if touch.touching then
    if touch.x<0 then left = 1 else right = 1 end
  end
  
  if keyboard.LEFT then left = 1 elsif keyboard.RIGHT then right = 1 end
  if gamepad.LEFT then left = 1 elsif gamepad.RIGHT then right = 1 end
  if left then dx += (-7-dx)*.1 elsif right then dx += (7-dx)*.1 else dx *= .9 end
  if left or right then speed *= .998 end
  x += dx
  horizon -= turn*.02*speed/50
  
  for c in clouds
    c.x -= turn*.02*speed/50*(1+(c.y-30)/300)
    c.x += c.v
    if c.x>screen.width/2+c.size then
      c.x -= screen.width+c.size
    elsif c.x<-screen.width/2-c.size then
      c.x += screen.width+c.size
    end
  end

  position += .02*speed/50
  x -= turn*0.04*speed*speed/40/40
  if abs(x)>230 then speed *= .98 x *= .99 end

  if position>=trackValue.length then
    position -= trackValue.length
  end
  
  for c in ai_cars
    c.position += c.speed*.02/50
    if c.position>trackValue.length then
      c.position -= trackValue.length
    end
  end
end

fmod = function(x,y)
  local i = floor(x/y)
  return x-i*y
end

lineToDistance = function(line)
  return 10/((100-line)+1)
end

distanceToLine = function(distance)
  return 101-10/distance
end




</script></html>